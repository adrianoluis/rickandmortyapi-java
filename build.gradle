plugins {
	id 'java'
	id 'jacoco'
	id 'maven-publish'

	id 'io.freefair.lombok' version '6.0.0-m2'
}

ext {
	gsonVersion = '2.8.7'
	guavaVersion = '30.1.1-jre'
	jerseyVersion = '2.34'
	slf4jVersion = '1.7.31'
	junitVersion = '4.13.1'
}

group = 'net.adrianoluis'
version = '1.1.3'

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.google.code.gson:gson:$gsonVersion"
	implementation "com.google.guava:guava:$guavaVersion"
	implementation "org.slf4j:slf4j-api:$slf4jVersion"
	implementation("org.slf4j:slf4j-log4j12:$slf4jVersion") { transitive = true }
	implementation("org.glassfish.jersey.core:jersey-common:$jerseyVersion") { transitive = true }

	testImplementation "junit:junit:$junitVersion"
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	withSourcesJar()
	withJavadocJar()
}

test {
	finalizedBy jacocoTestReport
	reports {
		html { enabled false }
	}
}

jacocoTestReport {
	dependsOn test
	reports {
		xml { enabled true }
		html { enabled false }
	}
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = 'https://maven.pkg.github.com/adrianoluis/rickandmortyapi-java'
			credentials {
				username = System.getenv('GH_USERNAME')
				password = System.getenv('GH_TOKEN')
			}
		}
		publications {
			gpr(MavenPublication) {
				from(components.java)
			}
		}
	}
}
